-- simple transmitter for DFPLAYR_Mini
--
-- This is free software: you can redistribute
-- it and/or modify it under the terms of the GNU General
-- Public License as published by the Free Software
-- Foundation, either version 3 of the License, or (at your
-- option) any later version.
--
-- This is distributed in the hope that it will
-- be useful, but WITHOUT ANY WARRANTY; without even the
-- implied warranty of MERCHANTABILITY or FITNESS FOR A
-- PARTICULAR PURPOSE. See the GNU General Public License
-- for more details.
--
-- Version 0.1
--
-- The general command structure consists of 10 bytes of which five are fixed, 
-- two are a checksum, and three specify the action (one is the command and two are parameters)
-- Format is
-- byte    function         value
-- (0)     start byte       0x7E
-- (1)     version info     0xFF
-- (2)     number of bytes  0x06
-- (3)     command          0x..  <- the command specifies by the user
-- (4)     command feedback 0x00  (if no feedback is required)
-- (5)     parameter 1 [DH] 0x..  <- 1st parameter (high-byte) specified by user
-- (6)     parameter 2 [DL] 0x..  <- 2nd parameter (low-byte) specified by user
-- (7)     checksum high    0x..  <- calculation is given below
-- (8)     checksum low     0x..  <- calculation is given below
-- (9)     end command      0xEF
--
-- checksum = - ( byte(1)+byte(2)+byte(3)+byte(4)+byte(5)+byte(6) )
-- and byte(7) and byte(8) are obtained as
-- checksumHigh = highByte(checksum)
-- checksumLow = lowByte(checksum) 
--
-- Statemaschine FSM
--
-- States:
-- RST_State: 
-- Wait: wait for 'do_send' & BUSY (which is LOW while a file is playing)
-- Sending: send complete command
-- goto Wait State
--
-- v0.2 RecelFA version with Background music only
-- v0.3 changed handling
-- v0.4 wait 3s after reset, then send DFPlayer reset command (0x0C) once,
--      wait another 3s before trigger detection is armed
-- v0.5 send complete 10 byte frames including checksum, some clone chips insist on it.
--      Send a harmless query (0x42) as very first frame, some clone chips drop the first
--      frame after power up. Follow up command mechanism, used for that query and to
--      kick playback with 0x0D after 0x11
-- v0.6 dropped the reset command (0x0C). The player boots together with the machine anyway,
--      and 0x0C only sent it through a second boot with SD scan, which is exactly when a
--      command gets lost. Now 5s wait, query (0x42) and stop (0x16) instead - 0x16 gives a
--      defined state without rebooting the player. This also supports very slow modules.


library ieee;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;


entity DFPlayer_Mini_CMD is
    Port ( trigger : in STD_LOGIC; --std_logic_vector (5 downto 0); -- 5 sounds plus background
           clk : in  STD_LOGIC; -- need to be fix 9600baud
           rst : in  STD_LOGIC; -- v0.4 boot_phase(0), so delay counters start right away
           txd : out  STD_LOGIC); --txd pin
end DFPlayer_Mini_CMD;

architecture Behavioral of DFPlayer_Mini_CMD is

function reverse_any_vector (a: in std_logic_vector)
    return std_logic_vector is
  variable result: std_logic_vector(a'RANGE);
  alias aa: std_logic_vector(a'REVERSE_RANGE) is a;
begin
  for i in aa'RANGE loop
    result(i) := aa(i);
  end loop;
  return result;
end; -- function reverse_any_vector

	signal DFcmd_cmd  : std_logic_vector (7 downto 0); -- the command for DFPlayer
	signal DFcmd_par1 : std_logic_vector (7 downto 0); --Parameter 1 (folder)
	signal DFcmd_par2 : std_logic_vector (7 downto 0); --Parameter 2 (soundnumber)
				 
	signal DFcmd_chk : unsigned (15 downto 0); -- v0.5 checksum -(byte1+..+byte6)

  signal command : std_logic_vector (99 downto 0);
  signal old_trigger :  STD_LOGIC;
  signal running :  STD_LOGIC;
  signal delay_cnt : integer range 0 to 48000; -- v0.4 delay counter
  signal pending :  STD_LOGIC; -- v0.5 follow up command waiting
  signal pending_cmd : std_logic_vector (7 downto 0); -- v0.5 the follow up command

  type STATE_T is ( Wait_1, Init, Idle, Send, Construct, Gap);
  signal state : STATE_T;        --State

  -- v0.6 5 seconds at 9600Hz dfp_clk, even a slow module is up by then
  constant DELAY_START : integer := 48000;
  -- v0.5 0.5 seconds between a command and its follow up command
  constant DELAY_GAP : integer := 4800;

  -- Structure of DFPlayer command over serial
  constant DFcmd_Start_Byte: unsigned (7 downto 0):=X"7E"; -- each command starts with 0x7E  
  constant DFcmd_Version: unsigned (7 downto 0):=X"FF"; -- version is always 0xFF
  constant DFcmd_len: unsigned (7 downto 0):=X"06"; -- len: number of bytes after len, always 6
  constant DFcmd_feedb: unsigned (7 downto 0):=X"00"; -- 0 == no feedback neededed
  constant DFcmd_End_Byte: unsigned (7 downto 0):=X"EF"; -- Ende Byte, always 0xEF

begin

-- v0.5 checksum = - ( byte(1)+byte(2)+byte(3)+byte(4)+byte(5)+byte(6) )
DFcmd_chk <= 0 - ( resize(DFcmd_Version,16) + resize(DFcmd_len,16)
                 + resize(unsigned(DFcmd_cmd),16) + resize(DFcmd_feedb,16)
                 + resize(unsigned(DFcmd_par1),16) + resize(unsigned(DFcmd_par2),16) );

DFPLAYR_Mini : process (clk, rst, trigger) is
  variable bitcounter : integer range 0 to 100 := 99;
begin
  if rst = '0' then --Reset condidition ( boot_phase(0) )
    txd <= '1';
	 old_trigger <= '0';
	 running <= '0';
	 delay_cnt <= 0;
	 pending <= '0'; -- v0.5 no follow up command
    state <= Wait_1;
  elsif rising_edge(clk)then
    case state is
	   when Wait_1 => -- v0.4 give DFPlayer time to boot after power on
				if delay_cnt < DELAY_START then
					delay_cnt <= delay_cnt + 1;
				else
					delay_cnt <= 0;
					state <= Init;
				end if;
	   when Init => -- v0.5 query status first, it primes the UART receiver of the DFPlayer.
				-- Some clone chips drop the very first frame after power up, and a query
				-- has no side effect at all ( the answer goes nowhere, TX is not connected )
				DFcmd_cmd <= X"42"; -- cmd for query current status
				DFcmd_par1 <= x"00"; -- fix
				DFcmd_par2 <= x"00"; -- fix
				pending_cmd <= X"16"; -- v0.6 followed by cmd for stop, gives a defined
				pending <= '1';       -- state without rebooting the player
				state <= Construct;
	   when Idle =>
			-- v0.4 old_trigger was not updated while waiting, so a trigger which came up
			-- during the startup sequence is caught up here with the very first compare
			if trigger /= old_trigger then					
					old_trigger <= trigger;				-- now reset trigger						
					state <= Construct;				
					if trigger = '1' then
						if running = '0' then
							DFcmd_cmd <= X"11"; -- cmd for Set all repeat playback
							DFcmd_par1 <= x"00"; -- fix
							DFcmd_par2 <= X"01"; -- fix
							pending_cmd <= X"0D"; -- v0.5 some chips only set the mode with
							pending <= '1';       -- 0x11, so kick playback with 'play' as well
							running <= '1';
						else
							DFcmd_cmd <= X"0D"; -- cmd for play
							DFcmd_par1 <= x"00"; -- fix
							DFcmd_par2 <= x"00"; -- fix		
						end if;
					else
						DFcmd_cmd <= X"0E"; -- cmd for pause
						DFcmd_par1 <= x"00"; -- fix
						DFcmd_par2 <= x"00"; -- fix				
					end if;
			end if; -- trigger /= old_trigger
		when Construct => 
			 --command(73 downto )
			 command <=   '0' & std_logic_vector(DFcmd_Start_Byte) & '1' -- startbyte is symmetric, no rverse needed
							& '0' & reverse_any_vector(std_logic_vector(DFcmd_Version)) & '1'
							& '0' & reverse_any_vector(std_logic_vector(DFcmd_len)) & '1'
							& '0' & reverse_any_vector(DFcmd_cmd) & '1'
							& '0' & reverse_any_vector(std_logic_vector(DFcmd_feedb)) & '1'
							& '0' & reverse_any_vector(DFcmd_par1) & '1'
							& '0' & reverse_any_vector(DFcmd_par2) & '1'
							& '0' & reverse_any_vector(std_logic_vector(DFcmd_chk(15 downto 8))) & '1'
							& '0' & reverse_any_vector(std_logic_vector(DFcmd_chk( 7 downto 0))) & '1'
							& '0' & reverse_any_vector(std_logic_vector(DFcmd_End_Byte)) & '1';
			 -- reset counter
			 bitcounter := 99; -- v0.5 complete frame including checksum
			 -- new state
          state <= Send;
      when Send =>
        txd <= command(bitcounter);
        bitcounter := bitcounter - 1;
        if bitcounter = 0 then
          if pending = '1' then -- v0.5 there is a follow up command to send
            delay_cnt <= 0;
            state <= Gap;
          else
            state <= Idle;
          end if;
        end if;
      when Gap => -- v0.5 short pause, then send the follow up command
        if delay_cnt < DELAY_GAP then
          delay_cnt <= delay_cnt + 1;
        else
          delay_cnt <= 0;
          DFcmd_cmd <= pending_cmd;
          DFcmd_par1 <= x"00"; -- fix
          DFcmd_par2 <= x"00"; -- fix
          pending <= '0';
          state <= Construct;
        end if;
      end case;
  end if;
end process;
end architecture;

